INIT     TITLE 'ARCHIVER --- WORK AREA/TIME/DATE SETUP '
*---------------------------------------------------------------------*
*---     (C) COPYRIGHT 1989, RICHARD A. FOCHTMAN JR., WOODRIDGE,   ---*
*---           ILLINOIS                                            ---*
*---                                                               ---*
*---     THIS SUBROUTINE HAS ONLY ONE PURPOSE IN THE ARCHIVER:     ---*
*---     SETUP THE CONSTANTS, ETC. NECESSARY TO THE BASIC          ---*
*---     FUNCTIONS IN GENERAL.  THAT IS, THE TIME, DATE, SYSIN     ---*
*---     AND SYSPRINT DCB'S, BLANK OUT THE VARIOUS PRINT LINE      ---*
*---     AREAS, ETC.  THIS IS PERFORMED ONCE FOR EACH RUN OF       ---*
*---     THE ARCHIVER.  FOR THAT REASON, THIS IS A SEPARATE        ---*
*---     LOAD MODULE OF THE ARCHIVER, USED AND THEN DELETED        ---*
*---     AT THE BEGINNING OF EACH ARCHIVER EXECUTION.              ---*
*---                                                               ---*
*---  RETRO-FITTED TO MVS 3.8J BY DAVE CARTWRIGHT AUGUST 2002      ---*
*---                                                               ---*
*---     PROGRAM ATTRIBUTES: NON-REENTRANT, NON-REUSABLE,          ---*
*---           NON-REFRESHABLE.                                    ---*
*--- UPDATE ON JULY 1,2020 ROB PRINS: CHANGE LA    R3,WALEN INTO   ---*
*---                                         L     R3,WALEN (STMNT 53)*
*--- UPDATE BY @RP AT 20200703: ADD SUPPORT FOR ALTERNATIVE        ---*
*--- DDNAMES FOR SYSPRINT AND SYSIN IN THE PARM OF THE EXEC        ---*
*--- STATEMENT IN THE ARCHIVER JCL.                                ---*
*---------------------------------------------------------------------*
ARCHINIT CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         USING ARCHINIT,R15
         #STAMP
         STM   R14,R12,12(R13)     STORE ENTRY REGISTERS
         LR    R11,R15             LOAD ROUTINE BASE REGISTER
         DROP  R15                 DROP THE OLD BASE
         USING ARCHINIT,R11        DEFINE THE NEW BASE REGISTER
         GETMAIN R,LV=72           GRAB STORAGE FOR SAVE AREA
         LA    R14,0(,R1)          KLUDGE FOR STD HDR
         ST    R14,8(,R13)         STORE FORWARD POINTER
         ST    R13,4(,R14)         AND BACK POINTER
         LR    R13,R14             LOAD NEW SAVE AREA POINTER
         L     R14,4(,R13)         RELOAD BACK POINTER
         L     R14,12(,R14)        RELOAD ORIG. REG 14
         COPY  #ARCBITS            COPY THE BIT-DEFINES
         L     R3,WALEN            LOAD SIZE OF WORK AREA     @RP200701
         GETMAIN R,LV=(3)          GRAB ANOTHER CHUNK OF STORAGE
         LR    R2,R1               COPY ITS ADDRESS
         LR    R4,R2
         SR    R5,R5
         MVCL  R2,R4               AND CLEAR IT OUT
         LR    R12,R1              AND LOAD WORK-AREA DSECT BASE
         USING MAINWK,R12          BASE THE WORK-AREA DSECT
         MVC   WKSYSPR,=CL8'SYSPRINT' DEFAULT DDNAME SYSRINT  @RP200703
         MVC   WKSYSIN,=CL8'SYSIN'    DEFAULT DDNAME SYSRINT  @RP200703
         MVC   WKKEYW,=CL8'SYSIN'  MOVE SYSIN= KEYWORD        @RP200703
         LA    R3,WKKEYW           1ST PARM ARCHPRSC          @RP200703
         LA    R4,WKWORKA          2ND PARM ARCHPRSC          @RP200703
         CALL  ARCHPRSC,((3),(4))  OBTAIN ALT. DDNAME SYSIN   @RP200703
         LTR   R15,R15             NEW DDNAME OF SYSIN?       @RP200703
         BNZ   NOALTIN             NO: CARRY                  @RP200703
         MVC   WKSYSIN,WKWORKA+2   MOVE ALTERNATIVE DDNAME    @RP200703
NOALTIN  DS    0H                                             @RP200703
         MVC   WKKEYW,=CL8'SYSPRINT' MOVE SYSPRINT= KEYWORD   @RP200703
         LA    R3,WKKEYW           1ST PARM ARCHPRSC          @RP200703
         LA    R4,WKWORKA          2ND PARM ARCHPRSC          @RP200703
         CALL  ARCHPRSC,((3),(4))  OBTAIN ALT. DDNAME SYSIN   @RP200703
         LTR   R15,R15             NEW DDNAME OF SYSIN?       @RP200703
         BNZ   NOALTPR             NO: CARRY                  @RP200703
         MVC   WKSYSPR,WKWORKA+2   MOVE ALTERNATIVE DDNAME    @RP200703
NOALTPR  DS    0H                                             @RP200703
         MVC   IOSEP,=X'47F0E000'  NO SEPARATOR ROUTINES
         MVC   ENDSEP,=X'47F0E000'   DEFINED YET ---
         MVC   MAINSIZE,WALEN      COPY SIZE INTO WORD 1
         ST    R12,ARCHWK          SAVE MAIN WORK-AREA POINTER
         MVC   EYEBALL,=CL4'ARCW'
         #SET  REPL,ON              REPLACE DUPS. DURING RELOAD   DFLT
         #SET  RETA,ON              RETAIN ALIAS INFORMATION      DFLT
         #SET  SHHDR,ON             PROCESS HEADERS               DFLT
         #SET  ULMODS,ON            RECFM=U MEANS LMODS           DFLT
         MVI   TITLE1,C' '         START WITH A BLANK
         MVC   TITLE1+1(120),TITLE1 CLEAR THE AREA
         MVC   TITLE2,TITLE1       ALSO SECOND AND
         MVC   TITLE3,INIT14       COPY IN THE USUAL SUBTITLE
         MVC   LINE,TITLE1         ALSO OUTPUT LINE AREA
         XC    TODVAL(16),TODVAL   CLEAR THE RETURN AREA
         EXTRACT TIOTA,'S',FIELDS=TIOT,MF=(E,CALLIST) LOCATE THE TIOT
         L     R1,TIOTA            LOAD THE TIOT POINTER
         LA    R1,24(,R1)          POINT TO THE FIRST TIOE
         ST    R1,TIOTA            SAVE UPDATED ADDRESS
*---------------------------------------------------------------------*
*---     FIND OUT IF SYSPUNCH IS PRESENT                           ---*
*---------------------------------------------------------------------*
LOCPNCH  DS    0H
         CLI   0(R1),0             END OF TIOT ??
         BE    NOPNCH              YES ---
         CLC   4(8,R1),=CL8'SYSPUNCH'  IS DDNAME SYSPUNCH ??
         BE    GOTPNCH             YES ---
         LA    R15,0               CLEAR A WORK REGISTER
         IC    R15,0(,R1)          INSERT TIOT LENGTH
         AR    R1,R15              POINT TO NEXT TIOE
         B     LOCPNCH
GOTPNCH  DS    0H
         #SET  PUNCH,ON
NOPNCH   DS    0H
         LA    R0,INIT7            LOAD THE LENGTH OF A QSAM DCB
         AH    R0,=H'4'            ADD A WORD
         GETMAIN R,LV=(0)          AND GRAB IT'S CORE
         ST    R0,0(,R1)           STORE AREA LENGTH
         LA    R1,4(,R1)
         MVC   0(INIT7,R1),INIT6   MOVE IN THE DCB
         ST    R1,PRTDCB           STORE IN THE WORK AREA
         LR    R3,R1               AND COPY IT'S ADDRESS
         USING IHADCB,R3           ADDRESS DCBD               @RP200703
         MVC   DCBDDNAM(8),WKSYSPR MOVE DDNAME OF SYSPRINT    @RP200703
         DROP  R3                                             @RP200703
         MVI   PRTDCB,X'80'        LAST-ENTRY FLAG FOR OPEN PARMLIST
         OPEN  ((3),OUTPUT),MF=(E,PRTDCB)      OPEN THE LISTING DCB
         TM    48(R3),X'10'        DID SYSPRINT OPEN ??
         BO    INIT1               YES, NOW CHECK SYSIN FLAG
         WTO   'SYSPRINT DCB DID NOT OPEN. CHECK DD STATEMENT'
         LA    R3,0
         ABEND 1
INIT1    DS    0H
         LA    R0,INIT9            LOAD DCB LENGTH
         AH    R0,=H'4'            ADD A WORD
         GETMAIN R,LV=(0)          GRAB STORAGE FOR DCB
         ST    R0,0(,R1)           STORE AREA LENGTH
         LA    R1,4(,R1)
         ST    R1,CTLIN            SAVE ITS ADDRESS
         LR    R2,R1               COPY DCB ADDRESS           @RP200703
         MVC   0(INIT9,R2),INIT8 MOVE IN THE DCB              @RP200702
         MVI   CTLIN,X'80'         FLAG END OF OPEN LIST
         USING IHADCB,R2           ADDRESS DCBD               @RP200703
         MVC   DCBDDNAM(8),WKSYSIN  MOVE DDNAME SYSIN         @RP200703
         DROP  R2                                             @RP200703
         LR    R1,R2                                          @RP200703
         OPEN  (,INPUT),MF=(E,CTLIN) AND OPEN THE DCB
         ZAP   PAGENO,=P'0'        INIT THE PAGE COUNTER
         ZAP   LSTLIN#,=P'99'      FORCE NEW-PAGE CONTROL
         ZAP   LINECNT,=P'60'      MOVE IN THE DEFAULT
         EJECT ,
         TIME  DEC                 OLD STYLE TIME MACRO
         STM   R0,R1,TODVAL        SAVE THE TIME VALUE
         SRDL  R0,40               DROP OFF THE FRACTIONS
         SLL   R1,4                MAKE FROM FOR DEC. SIGN VALUE
         O     R1,=A(X'0F')        MAKE A POSITIVE SIGN
         STM   R0,R1,TIME          SAVE IN CLOCK VALUE
         SPACE 1
         MVO   PYYYY,TODVAL+4(2)   COPY YEARS
         OI    PYYYY+1,X'0C'       MAKE POSITIVE
         ZAP   TODVAL+8(8),P1900   SET BASE YEAR
         AP    TODVAL+8(8),PYYYY   CALCULATE CALENDAR YEAR
         CVB   R15,TODVAL+8        CONVERT TO BINARY
         MVO   DATE(3),TODVAL+13(3) LEFT SHIFT ANSWER
         ICM   R0,B'0011',DATE     PICK UP YEAR DIGITS
         ST    R15,TODVAL+8        SAVE BINARY YEARS
         LA    R1,DAYS-2           POINTER TO MONTHS
         ZAP   DATE,TODVAL+6(2)    GET DAY NUMBER
         CVB   R15,DATE            .. AS BINARY
         TM    TODVAL+11,X'03'     TEST FOR LEAP YEAR
         BZ    LEAPYEAR            IT IS, ADJUST POINTER
         LA    R2,DAYS-6           POINT TO DAYS
         B     MNTHLOOP            GO FIND MONTH
LEAPYEAR DS    0H                  THIS IS A LEAPYEAR
         LA    R2,DAYS-4           POINT TO L/Y DAYS
MNTHLOOP DS    0H                  FIND WHICH MONTH WE ARE
         LA    R2,6(,R2)           BUMP ADDRESS OF DAY
         LA    R1,6(,R1)           BUMP ADDRESS OF MONTH
         CH    R15,0(,R2)          COMPARE DATE WITH DAYS
         BNH   MNTHLOOP            NOT IN THS MONTH
         ICM   R0,B'1000',1(R1)    COPY MONTH NUMBER
         SH    R15,0(,R2)          OTHERWISE SUBTRACT DAYS
         CVD   R15,DATE            GET DAY NUMBER
         MVO   TODVAL(2),DATE+6(2) LEFT SHIFT IT
         ICM   R0,B'0100',TODVAL   PUT INTO DATE
         SRDL  R0,28               SHIFT DOWN THE BUS
         O     R1,=A(X'0F')        MAKE A POSITIVE SIGN
         STM   R0,R1,DATE          SAVE IN DATE VALUE
         EJECT ,
         LR    R15,R12             COPY THE WORK AREA ADDRESS
         LR    R1,R13              COPY THE SAVE AREA ADDRESS
         L     R13,4(,R13)         POINT TO PREVIOUS S.A.
         ST    R15,16(,R13)        PASS W.A. ADDR AS RETURN CODE
         FREEMAIN R,LV=72,A=(1)    FREE THE SAVE AREA
         LM    R14,R12,12(R13)     RESTORE ENTRY REGISTERS
         MVI   12(R13),255
         BR    R14                 AND RETURN TO ARCHCTL
         LTORG ,                   DISPLAY THE LITERALS SO FAR
         SPACE 3
P1900    DC    PL3'1900'           START YEAR
PYYYY    DC    PL2'0'              YEAR
         SPACE 3
INIT6    DCB   DSORG=PS,                                               X
               MACRF=PM,                                               X
               EXLST=INIT10,                                           X
               DDNAME=SYSPRINT,                                        X
               RECFM=FBA,                                              X
               LRECL=133,                                              X
               EROPT=ABE
INIT7    EQU   *-INIT6
INIT8    DCB   DSORG=PS,                                               X
               MACRF=GM,                                               X
               EXLST=INIT10,                                           X
               DDNAME=SYSIN,                                           X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               EROPT=ABE
INIT9    EQU   *-INIT8
INIT10   DC    0F'0'
         DC    X'85',AL3(INIT11)
         PUSH  USING
         USING INIT11,R15
INIT11   DS    0H
         LH    R6,62(,R1)          LOAD DCB BLKSIZE ??
         LTR   R6,R6               IS BLKSIZE STILL ZERO ??
         BNZ   INIT12              NO; SEE IF MULTIPLE OF LRECL
         MVC   62(2,R1),82(R1)     COPY LRECL TO BLKSIZE
         BR    R14                 RETURN TO OPEN/CLOSE/EOV
INIT12   DS    0H
         SRDA  R6,32               SHIFT BLKSIZE TO DIVIDEND REGISTER
         LH    R5,82(,R1)          LOAD LRECL VALUE
         DR    R6,R5               COMPUTE BLOCKING FACTOR
         LTR   R7,R7               QUOTIENT ZERO ??
         BNZ   INIT13              NOPE ---
         LA    R7,1                ASSUME 1 RECORD/BLOCK
INIT13   DS    0H
         MH    R7,82(,R1)          TIMES LRECL
         STH   R7,62(,R1)          STORE COMPUTED BLOCK SIZE
         BR    R14                 RETURN TO OPEN/CLOSE/EOV
         DROP  R15
         POP   USING
INIT14   DC    CL2' '
         DC    CL10'  MEMBER '                                     HEAD
         DC    CL1'-'
         DC    CL10'  GROUP  '                                     HEAD
         DC    CL1'-'
         DC    CL10'  SUBGRP '                                     HEAD
         DC    CL1'-'
         DC    CL10'   TYPE  '                                     HEAD
         DC    CL1'-'
         DC    CL12'       VRSN '                                  HEAD
         DC    CL8' ACTION '
         DC    CL70' '
DTIME    DS    F
DDATE    DS    F
WALEN    DC    A(ENDMAIN-MAINWK)
         SPACE 2
DAYS     DC    AL2(334),AL2(335)        NOV
         DC    XL2'0012'
         DC    AL2(304),AL2(305)        OCT
         DC    XL2'0011'
         DC    AL2(273),AL2(274)        SEP
         DC    XL2'0010'
         DC    AL2(243),AL2(244)        AUG
         DC    XL2'0009'
         DC    AL2(212),AL2(213)        JUL
         DC    XL2'0008'
         DC    AL2(181),AL2(182)        JUN
         DC    XL2'0007'
         DC    AL2(151),AL2(152)        MAY
         DC    XL2'0006'
         DC    AL2(120),AL2(121)        APR
         DC    XL2'0005'
         DC    AL2(090),AL2(091)        MAR
         DC    XL2'0004'
         DC    AL2(059),AL2(060)        FEB
         DC    XL2'0003'
         DC    AL2(031),AL2(031)        JAN
         DC    XL2'0002'
         DC    AL2(000),AL2(000)
         DC    XL2'0001'
         COPY  ##ARCHWK
         DCBD  DSORG=(PS,PO)
         END
